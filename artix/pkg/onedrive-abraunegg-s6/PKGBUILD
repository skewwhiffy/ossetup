# Maintainer: Kenny Hung
pkgname=onedrive-abraunegg-s6
pkgver=20200404
pkgrel=1
pkgdesc="s6 service scripts for onedrive-abraunegg (kenny user only)"
arch=('any')
url="https://github.com/artix-linux"
groups=('s6-world')
provides=('onedrive-abraunegg-runscripts')
conflicts=('onedrive-abraunegg-runscripts')
depends=('onedrive-abraunegg-git' 's6')
install=onedrive.install
backup=('etc/s6/sv/onedrive-log/conf')
source=("onedrive-log.conf"
        "onedrive-log.consumer-for"
        "onedrive-log.notification-fd"
        "onedrive-log.pipeline-name"
        "onedrive-log.run"
        "onedrive-log.type"
        "onedrive-srv.producer-for"
        "onedrive-srv.run"
        "onedrive-srv.type")

sha256sums=('7bd1fe2c69a7aab98655c7600e958d8fdb5d0e39603c2e0cef2eb3c0434eb51c'
            '6d79488d3133acb34f56b3db857cf96d4b4ac46bb56aa0309b8abc3f798c3c13'
            '1121cfccd5913f0a63fec40a6ffd44ea64f9dc135c66634ba001d10bcf4302a2'
            '919e09dd71ded009e5006e9cae5b2e520efa6e710c9159cc6d01297accba4f6a'
            '71425bc5ed3053345cb32281c8987ff0608aad3ba381a40d1124547ac4082994'
            'd0001a150b83f68f09004c5059045cb76a3f064eed4d42ce072bc3722c118006'
            'd7cabc007cd28a4a88ae06c482b01e360aa6ab7f508f33bae6e88c6ac0e6d7bd'
            '8992d2e1cb67b538e55abb20a0af779ffc1a824b7c1b3e96a7ed1f592e4097ba'
            'd0001a150b83f68f09004c5059045cb76a3f064eed4d42ce072bc3722c118006')

_inst_sv(){
    for file in conf consumer-for dependencies notification-fd \
        pipeline-name producer-for run type; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm644 "$srcdir/$1.$file" "$pkgdir/etc/s6/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'onedrive-log'
    _inst_sv 'onedrive-srv'
}
