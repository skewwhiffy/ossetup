# Maintainer: Kenny Hung

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=onedrive-abraunegg-runit
pkgver=20191208
pkgrel=1
pkgdesc="runit service scripts for onedrive-abraunegg (kenny user only)"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
depends=('onedrive-abraunegg' 'runit')
provides=('onedrive-abraunegg-runscripts')
replaces=('onedrive-abraunegg-runscripts')
source=("onedrive.run" "logonedrive.run")
sha256sums=('e91c16bbd432eb79a6edcc35a52da4c630b8ab8da34390130a12a19978250923'
  '78f59baa9d291a6c72455d47807ac51cabd9f7f006d64472df1c8a4d38396b01')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'onedrive'
    sed -e 's|onedrive.pid|onedrive/pid|g' -i "${pkgdir}/etc/runit/sv/onedrive"/*
    _inst_logsv 'onedrive'

    install -d "${pkgdir}/etc/runit/runsvdir/default"
    ln -sf "/etc/runit/sv/onedrive" "${pkgdir}/etc/runit/runsvdir/default/onedrive"
}
